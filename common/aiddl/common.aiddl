(#mod self org.aiddl.common)

(#nms EVAL org.aiddl.eval)

(#def Map
  (and
    (type #self {list set})
      (forall ?e #self
        (and
          (type ?e key-value) ))))
            
(#def MapGen
  (match (?KT ?VT) #self
    (lambda ?M ($MapOf (?KT ?VT ?M)))))

(#def MapOf
  (match (?KeyType ?ValueType ?Map) #self
    (and
      (type ?Map {^list ^set})
      (forall ?k:?v ?Map
        (and
          (type ?k ?KeyType)
          (type ?v ?ValueType)
       )))))

(#def SetGen
  (lambda ?Set
    (and
      (type ?Set ^set)
      (forall ?e ?Set (type ?e #self)))))

(#def (SetOf ?Type ?Set)
  (and
    (type ?Set ^set)
    (forall ?e ?Set (type ?e ?Type))))

(#def ListGen
  (lambda ?List
    (and
      (type ?List ^list)
      (forall ?e ?List (type ?e #self)))))

(#def (ListOf ?Type)
  (and
    (type #self ^list)
    (forall ?e #self (type ?e ?Type))))


(#def Composite
  (match (?S ?Arg) #self
    (and
      (type ?S ^collection)
      (forall ?e ?S (type ?Arg ?e))
    )))

(#def KeyValuedType
  (lambda ?x
    (forall ?k:?t #self
      (call ?t (get-key ?k ?x))) ))

(#def TypedTuple
  (lambda ?x
    (forall ?i (domain {min:0 inc:1 max:(- (size #self) 1)})
      (type (get-idx ?i ?x) (get-idx ?i #self))
)))


(#def MapFunctionGen
  (lambda ?k
    (get-key ?k #self default:NIL)))
      