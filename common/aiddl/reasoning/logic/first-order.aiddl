(#mod self org.aiddl.common.reasoning.logic.first-order)

(#namespace EVAL org.aiddl.eval-ht)

(#type Formula
  (#type.union [ ^$Atom ^$NegatedFormula ^$ConnectedFormula ^$QuantifiedFormula ]))

(#type NegatedFormula
  (#type.sig [^$Negation ^$Formula]))

(#type ConnectedFormula
  (#type.sig [^$Connective ^$Formula ^$Formula]))

(#type QuantifiedFormula
  (#type.sig [^$Quantifier ^$Variable ^$Formula]))

(#type Atom (#type.sig [^$Predicate ^$Term] min:1 max:INF))
(#type FunctionTerm (#type.sig [^$Function ^$Term] min:1 max:INF))
 
(#type Term (#type.union [^$Constant  ^$Variable ^$FunctionTerm]))

(#type Negation (#type.enum { ! }))
(#type Connective (#type.enum { => <= <=> ^ v }))
(#type Quantifier (#type.enum { EXISTS FORALL }))

(#type Constant  (#type.union [#symbolic #string #integer #rational #real]))
(#type Variable  #variable)
(#type Predicate #symbolic)
(#type Function  #symbolic)
