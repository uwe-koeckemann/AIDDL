(#mod self org.aiddl.common.reasoning.logic.propositional)

(#req EVAL org.aiddl.eval)

(#def Boolean
  (and@EVAL
    (has-type@EVAL #self ^symbolic)
    (in@EVAL #self {true false})
  ))

(#def BooleanVariable
  (has-type@EVAL #self ^term) )

(#def BooleanOperator
  (and@EVAL
    (has-type@EVAL #self ^symbolic)
    (in@EVAL #self {and or & | => <= <=>})))

(#def BooleanFormula
  (and@EVAL
    (has-type@EVAL #self ^tuple)
    (signature@EVAL #self [^$BooleanOperator ^$BooleanExpression])
    (=@EVAL (size@EVAL #self) 3) ))

(#def BooleanFormulaNegation
  (and@EVAL
    (has-type@EVAL #self ^tuple)
    (=@EVAL (size@EVAL #self) 2)
    (or@EVAL
      (match@EVAL (not ?F) #self (has-type@EVAL ?F ^$BooleanExpression))
      (match@EVAL (! ?F) #self (has-type@EVAL ?F ^$BooleanExpression))
    )))

(#def BooleanExpression
  (or@EVAL
    (has-type@EVAL #self ^$Boolean)
    (has-type@EVAL #self ^$BooleanFormula)
    (has-type@EVAL #self ^$BooleanFormulaNegation)
    (has-type@EVAL #self ^$BooleanVariable)
  ))

(#def KnowledgeBase
  (and@EVAL
    (has-type@EVAL #self ^set)
    (forall@EVAL ?e #self
      (has-type@EVAL ?e ^$BooleanExpression) )))

(#def CNF
  (and@EVAL
    (has-type@EVAL #self ^set)
    (forall@EVAL ?e #self
      (and@EVAL
        (has-type@EVAL ?e ^set) ))))
