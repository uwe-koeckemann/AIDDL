(#mod self org.aiddl.common.reasoning.resource)

(#req EVAL org.aiddl.eval.namespace)
(#nms E hashtag@EVAL)

(#req C org.aiddl.common)
(#req FoL org.aiddl.common.reasoning.logic.first-order)
(#req AC org.aiddl.common.reasoning.temporal.allen-interval)
(#req STP org.aiddl.common.reasoning.temporal.stp)

(#def Resource
  (#has-type #self {^#symbolic Atom@FoL}))

(#def Activity
  (#has-type #self {^#symbolic Atom@FoL}))

(#def Capacity
  (#match ?Resource:?Cap #self
    (#and
      (#has-type ?Resource $Resource)
      (#let [?Min:(#get-key min ?Cap) ?Max:(#get-key max ?Cap)]
        (#and
          (#has-type ?Min #integer)
          (#has-type ?Max #integer) )))))

(#def Change
  (#and
    (#has-type #self #tuple)
    (#match ?Resource:(?Op ?Amount) #self
      (#and
        (#has-type ?Resource $Resource)
        (#in ?Op {+ - =})
        (#has-type ?Amount #integer) ))))

(#def Peak
  (#has-type #self (SetGen@C ^$Activity)))

(#def UsageTuple
  (#and
    (#size #self 3)
    (#signature #self [Interval@AC $Resource #integer])))

(#def TupleUsages
  (#has-type #self (SetGen@C ^$UsageTuple)))

(#def Usages
    (#has-type #self (MapGen@C ^$Activity ^$integer)))

(#def RcpspCapacities
  (#forall ?cap #self
    (#and
      (#has-type ?cap ^$Capacity)
      (#equals 0 (#get-key min ?cap)) )))

(#def RCPSP
  (#let [
          ?RC:(#get-key capacity #self)
          ?RU:(#get-key usage #self)
          ?AC:(#get-key constraints #self) ]
    (#and
      (#forall ?cap ?RC
        (#and
          (#has-type ?cap ^$Capacity)
          (#equals 0 (#get-key min ?cap)) ))
      (#forall ?resource:?usage ?RU
        (#and
          (#has-type ?resource ^$Resource)
          (#has-type ?usage ^$Usage) ))
      (#has-type ?AC ^AllenConstraints@AC) )))


(#interface variable-value-ordering
  (
    uri:(#sym-concat $self variable-value-ordering)
    input:(TypedTuple@C (SetGen@C ^$Peak) ^Solution@STP) 
    output:(ListGen@C ^AllenConstraint@AC)))

(#interface mcs-collector
  (
    uri:(#sym-concat $self peak-collector)
    input:(KeyValuedType@C usage:^$Usage times:^IntervalDomainLookup@STP) 
    output:(SetGen@C ^$Peak)))