(#mod self org.aiddl.test.types)

(#req nms org.aiddl.eval.namespace)
(#namespace nms-basic basic-ops@nms)
(#namespace nms-logic logic-ops@nms)
(#namespace nms-type type-ops@nms)

(#type const-type (enum  {NIL}))

(#assert type-test-const-1 ($const-type NIL))
(#assert type-test-const-2 (not ($const-type LIN)))

;; Link to basic AIDDL type:
(#type basic org.aiddl.type.term.symbolic)

;; Type is a set of another type:
(#type set-of-sym (set-of $basic))
(#type col-of-sym (col-of $basic))

($set-of-sym set-1 {a b c})
($set-of-sym set-2 {a b c 1 2 3})

(#assert type-test-1 ($set-of-sym $set-1))
(#assert type-test-2 (not ($set-of-sym $set-2)))
(#assert type-test-1 ($col-of-sym $set-1))
(#assert type-test-2 (not ($col-of-sym $set-2)))


;; Fixed domain enumeration
(#type enum-type
  (enum {a b c d}))

(#assert type-test-3 ($enum-type b))
(#assert type-test-4 (not ($enum-type e)))

(#type list-of-enum (list-of $enum-type))

(#assert type-test-5 ($list-of-enum [a b c a a a c d b b]))
(#assert type-test-6 ($list-of-enum []))
(#assert type-test-7 (not ($list-of-enum [a b c a a e a c d b b])))

;; Tuple with minimum and maximum number of elements and signature
(#type function-term (sig [symbolic org.aiddl.test.types.fol-term] min:1 max:INF))
(#type fol-term   (union [$function-term org.aiddl.type.term.symbolic org.aiddl.type.term.numerical org.aiddl.type.term.variable]))
(#type atomic (union [symbolic $function-term]))

(#assert type-test-8 ($atomic p))
(#assert type-test-9 ($atomic (p x 1 ?y)))

(#type point (sig [^numerical ^numerical] min:2 max:2))

(#assert type-test-point
  [
    (not ($point (1)))
    (not ($point (1 2 3)))
    (not ($point 1))
    ($point (1 2))
  ])


(#type matrix-1 (matrix m:2 n:2 col-types:(^numerical ^numerical) row-types:(^numerical ^numerical) cell-type:^numerical))

(#assert type-test-matrix
  [
    (not ($matrix-1 {a 1}))
    (not ($matrix-1 ((1))))
    (not ($matrix-1 ((1 2) (a b))))
    ($matrix-1 ((1 2) (3 4)))
  ])


(#type matrix-2 (matrix m:2 n:2))

(#assert type-test-matrix
  [
    (not ($matrix-2 ((1))))
    ($matrix-2 ((1 2) (a b)))
    ($matrix-2 ((1 2) (3 4)))
  ])

;; Key and value have a type
(#type sva
   (typed-kvp $atomic:$atomic))

(#assert type-test-10
  [
    ($sva (p x y):z)
    ($sva x:v)
    ($sva c:(p x ?a))
    (not ($sva (p x ?a)))
    (not ($sva x))
  ])

(#type state (set-of $sva))
(#type goal  (set-of $sva))

;; Fixed keys with value types:
(#type operator
  (dict
    [
      name:$atomic
      precondition:$state
      effect:$state]))

(#assert type-test-11
  [
    ($operator (name:op1 precondition:{x:a y:b} effect:{x:b}))
    (not ($operator (precondition:{x:a y:b} effect:{x:b})))
    (not ($operator (name:op1 effect:{x:b})))
    (not ($operator (name:op1 precondition:{x:a y:b})))
  ])

(#type problem
   (dict
     [
       initial-state:$state
       goal:$goal
       operators:(set-of $operator)
     ]))

(#assert type-test-12
  ($problem
    (
      initial-state:{(p r t):x x:v t:r}
      goal:{x:a y:b z:c}
      operators:{(name:op1 precondition:{x:a y:b} effect:{x:b})}
    )))

;; Real number range
(#type my-range
  (range min:-1.0 max:1.0))   

(#assert type-test-n-2 ($my-range 0.1))
(#assert type-test-n-1 ($my-range -0.5))
(#assert type-test-n   (not ($my-range 1.5)))
(#assert type-test-n   (not ($my-range x)))

(#def my-con (match (?A ?B) #arg (not-equals ?A ?B)))

(#type not-equal-type (sig [$enum-type $enum-type] min:2 max:2 constraint:^$my-con))

(#assert type-test-con-1 ($not-equal-type      (a b)))
(#assert type-test-con-2 (not ($not-equal-type (a a))))
(#assert type-test-con-3 (not ($not-equal-type (b b))))
(#assert type-test-con-4 (not ($not-equal-type (c c))))

(#type (gen-set ?T) (set-of ?T))

(#assert gen-type-test-01 (call ($gen-set ^integer) {1 2 3}))
(#assert gen-type-test-02 (not (call ($gen-set ^integer) {1 2 3 a})))

(#type type-pair (sig [^symbolic ^symbolic] constraint:^!=))

(#assert test-type-pair-01 ($type-pair (a b)))
(#assert test-type-pair-02 (not ($type-pair (a a))))

(#type type-inter (inter {(enum {a b}) (enum {b c})}))

(#assert test-type-inter-01 ($type-inter b))
(#assert test-type-inter-02 (not ($type-inter a)))
(#assert test-type-inter-03 (not ($type-inter c)))

(#type type-dict-with-opt
   (dict
     [
       a:^symbolic
       b:^numerical
     ]
     optional:[
       c:^numerical
     ]
    ))

(#assert type-test-dict-opt-1 ($type-dict-with-opt {a:x b:2}))
(#assert type-test-dict-opt-2 ($type-dict-with-opt {a:x b:2 c:42}))
(#assert type-test-dict-opt-3 (not ($type-dict-with-opt {a:x b:2 c:y})))